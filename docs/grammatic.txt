=====================================
    This is grammatic of MySwift:
=====================================

==================
    TODO LIST:

==================

<program> ::=
    <global-list>

<global-list> ::=
    <class-declaration> [ <global-list> ] |
    <function-declaration> [ <global-list> ] |
    <variable-declaration> [ <global-list> ] |
    <constant-declaration> [ <global-list> ]

<class-declaration> ::=
    "class" <class-name> [ <type-inheritance-clause> ] <class-body>

<type-inheritance-clause> ::=
    ":" <type-inheritance-list>

<type-inheritance-list> ::=
    <type-identifier> |
    <type-identifier> , <type-inheritance-list>

<class-name> ::=
    <identifier>

<class-body> ::=
    "{" <class-members> "}"

<class-members> ::=
    <class-member> [ <class-members> ]

<class-member> ::=
    <declaration>

<extension-declaration> ::=
    "extension" <type-identifier> [ <type-inheritance-clause> ] <extension-body>

<extension-body> ::=
    "{" <extension-members> "}"

<extension-members> ::=
    <extension-member> [ <extension-members> ]

<extension-member> ::=
    <declaration>

<expression-list> ::=
    <expression> |
    <expression> "," <expression-list>

<type> ::=
    <type-identifier> |
    "[" <type-identifier> "]"

<type-identifier> ::=
    <type-name> |
    <type-name> "." <type-identifier>

<type-name> ::=
    <identifier> |
    "String" |
    "Int" |
    "Double" |
    "Bool"

<type-identifier> ::=
    <identifier>

<declarations> ::=
    <declaration> [ declarations ]

<declaration> ::= 
    <constant-declaration> |
    <varibale-declaration> |
    <function-declaration> |
    <class-declaration> |
    <initializer-declaration> |
    <deinitializer-declaration>

<constant-declaration> ::=
    "let" <pattern> <initializer>

<pattern-initializer-list> ::=
    <pattern-initializer> |
    <pattern-initializer> , <pattern-initializer-list>

<pattern-initializer> ::=
    <pattern> [ <initializer> ]

<initializer> ::=
    "=" <expression>

<pattern> ::=
    <wildcard-pattern> [ <type-annotation> ] |
    <identifier-pattern> [ <type-annotation> ]

<wildcard-pattern> ::=
    "_"

<identifier-pattern> ::=
    <identifier>

<type-annotation> ::=
    ":" <type> |
    ":" "[" <type> "]"

<variable-declaration> ::=
    "var" <pattern-initializer-list>

<function-declaration> ::=
    "func" <function-name> <function-declaration-argument-clause> [ <function-return-type> ] <code-block>

<function-declaration-argument-clause> ::=
    "(" [ <pattern-list> ] ")"

<pattern-list> ::=
    <pattern> |
    <pattern> <pattern-list>

<function-return-type> ::=
    "->" <type>

<initializer-declaration> ::=
    "init" <parameter-clause> <initializer-body>

<initializer-body> ::=
    <code-block>

<code-block> ::=
    "{" <statements> "}"

<statements> ::=
    <statement> [ <statements> ]

<statement> ::=
    <expression> [ ";" ] |
    <declaration> [ ";" ] |
    <loop-statement> [ ";" ] |
    <if-statement> [ ";" ] |
    <control-transfer-statement> [ ";" ]

<loop-statement> ::=
    <for-in-statement> |
    <while-statement>

<for-in-statement> ::=
    "for" <pattern> "in" "range" "(" <expression> ")" <code-block>

<while-statement> ::=
    "while" <condition> <code-block>

<condition> ::=
    <expression>

<if-statement> ::=
    "if" <condition> <code-block> [ <else-clause> ]

<else-clause> ::=
    "else" <code-block> |
    "else" <if-statement>

<control-transfer-statement> ::=
    "break"|
    "continue" |
    <return-statement>


<return-statement> ::=
    "return" [ <expression> ]

<expression> ::=
    <primary-expression> |
    <function-call-expression> |
    <explicit-member-expression> |
    <subcript-expression> |
    <prefix-operator> <expression> |
    <expression> <infix-operator> <expression> |
    <array-expression>

<array-expression> ::=
    "[" <expression>* "]"

<prefix-operator> ::=
    "-" |
    "!"

<infix-operator> ::=
    "+" | "*" | "/" | "+" | "and" | "or" | "="

<primary-expression> ::=
    <identifier> |
    "self" |
    <parenthesized-expression>

<parenthesized-expression> ::=
    "(" <expression> ")"

<function-call-expression> ::=
    <identifier> <function-call-argument-clause>

<function-call-argument-clause> ::=
    "(" [ <function-call-argument-list>  ] ")"
    
<function-call-argument-list> ::=
    <function-call-argument> |
    <function-call-argument> "," <function-call-argument-list>

<function-call-argument> ::=
    <expression>

<explicit-member-expression> ::=
    <expression> "." <explicit-member-argument>

<explicit-member-argument> ::=
    <identifier> |
    <function-call-expression>

<subcript-expression> ::=
    <expression> "[" <expression> "]"
